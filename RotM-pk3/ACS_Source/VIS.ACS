#library "VIS"
#include "zcommon.acs"

#DEFINE MAX_PLAYERS 64
#DEFINE TEAM_NONE -1
#DEFINE TEAM_COOP -2

function bool TeamVisualCvar(void)
{
	return (TeamVisualCvarServer()&&TeamVisualCvarClient());
}

function bool TeamVisualCvarClient(void)
{
	return (getUserCvar(ConsolePlayerNumber(),"rotm_teamvisual")>0);
}

function bool TeamVisualCvarServer(void)
{
	return (getCvar("rotm_sv_teamvisual")>0);
}


Script "TeamVisuals" OPEN CLIENTSIDE
{
while(timer()<35*2) { // Fixes crash when doing map changes online
	delay(1);
}
int conP = ConsolePlayerNumber();
bool prevTeamVisual = false;
while(true) {
	if(TeamVisualCvar()) {
		TeamVisualsFunc(conP);
		prevTeamVisual = true;
	} else if(prevTeamVisual) {
		GiveAllPlayers("TeamVisualOffFunc",1);
		prevTeamVisual = false;
	}
	delay(1);
}
}

function void TeamVisualsFunc(int conP) {
	int p = conP;
	int camera = CheckPlayerCamera(conP);
	if(camera>999&&camera<(1000+MAX_PLAYERS)) p = (camera-1000);
	int team = getPlayerTeam(p);
	if(team==TEAM_COOP) {
		GiveAllPlayers("AllyVisualFunc",1);
	} else if(team!=TEAM_NONE) {
		GiveTeamPlayers(team,"AllyVisualFunc","EnemyVisualFunc",1);
	} else {
		GiveFFAPlayers(p,"AllyVisualFunc","EnemyVisualFunc",1);
	}
}

function int getPlayerTeam(int p)
{
if (GetCVar("teamplay")==1||GetCVar("teamlms")==1||GetCVar("teampossession")==1
||GetCVar("teamgame")==1){
	Return GetPlayerInfo(p,PLAYERINFO_TEAM);
} else if (GetCVar("cooperative")==1) {
	Return TEAM_COOP;
}
Return TEAM_NONE;
}

function void GiveAllPlayers(str item, int amount)
{
for(int i=0;i<MAX_PLAYERS;i++) GiveActorInventory(1000+i,item,amount);
}

function void GiveTeamPlayers(int team, str allyItem, str enemyItem, int amount)
{
for(int i=0;i<MAX_PLAYERS;i++) {
	if(team==GetPlayerInfo(i, PLAYERINFO_TEAM)) GiveActorInventory(1000+i,allyItem,amount);
	else GiveActorInventory(1000+i,enemyItem,amount);
}
}

function void GiveFFAPlayers(int p, str allyItem, str enemyItem, int amount)
{
int i;
for(i=0;i<p;i++) GiveActorInventory(1000+i,enemyItem,amount);
GiveActorInventory(1000+i,allyItem,amount);
for(i=p+1;i<MAX_PLAYERS;i++) GiveActorInventory(1000+i,enemyItem,amount);
}